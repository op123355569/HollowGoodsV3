apply plugin: "maven"

def isReleaseBuild() {
    return !LIBRARY_VERSION.toUpperCase().contains("SNAPSHOT")
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

def getRepositoryUrl() {
    return isReleaseBuild() ? RELEASE_REPOSITORY_URL : SNAPSHOT_REPOSITORY_URL
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.groupId = GROUP_ID
                pom.artifactId = ARTIFACTID
                pom.version = LIBRARY_VERSION
                repository(url: getRepositoryUrl()) {
                    authentication(
                            userName: getRepositoryUsername(),
                            password: getRepositoryPassword()
                    )
                }
            }
        }
    }

    // Release不打入源码，Debug打入源码
    if (!isReleaseBuild()) {
        // 打包源码:Start
        task androidSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.srcDirs
        }
        artifacts {
            archives androidSourcesJar
        }
        // 打包源码:End
    }

}